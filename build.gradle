buildscript {
    repositories {
        maven {
            url 'https://maven.minecraftforge.net'
        }

        maven {
            url 'https://repo.spongepowered.org/maven'
        }

        mavenCentral()
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

group = 'net.foulest'
description = 'Helix'
version = '1.0.0-dep'
archivesBaseName = 'Optibye'

targetCompatibility = 1.8
sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'
    makeObfSourceJar = false
}

configurations {
    shade
    compile.extendsFrom(shade)
}

repositories {
    maven {
        url 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile 'org.spongepowered:mixin:0.8.7'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '26.0.2'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.38'
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'optibye.refmap.json'
}

jar {
    manifest {
        attributes(
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.optibye.json',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'TweakOrder': '0' // optional, but nice to be explicit
        )
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    classifier ''
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.optibye.json',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'TweakOrder': '0'
        )
    }
}

reobf {
    shadowJar {}
}
